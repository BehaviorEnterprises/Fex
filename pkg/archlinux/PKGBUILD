# Maintainer: Jesse AKA "Trilby" <jesse [at] mcclurek9 [dot] com>
_gitname="fex"
pkgname="${_gitname}-git"
pkgver=2.0
pkgrel=1
pkgdesc='Frequency Excursion Calculator'
url='http://github.com/TrilbyWhite/fex.git'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('cairo' 'desktop-file-utils' 'fftw' 'libxpm' 'libsndfile' 'python2' 'pygtk' 'sox')
makedepends=('git')
install="${_gitname}.install"
source=("${_gitname}::git://github.com/TrilbyWhite/fex.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_gitname}";
	echo "2.$(git rev-list --count HEAD).$(git describe --always )"
}

build() {
	cd "${_gitname}"
	make
}

package() {
	cd "${_gitname}"
	make DESTDIR="${pkgdir}" install
}
